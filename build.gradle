plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.4'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'RM'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	// Spring Boot Web + JPA
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

	// Spring Security (JWT 인증 처리용)
	implementation 'org.springframework.boot:spring-boot-starter-security'

	// JJWT (JWT 토큰 생성/검증)
	implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
	runtimeOnly   'io.jsonwebtoken:jjwt-impl:0.11.5'
	runtimeOnly   'io.jsonwebtoken:jjwt-jackson:0.11.5'

	// Jakarta Annotation for @PostConstruct
	implementation 'jakarta.annotation:jakarta.annotation-api:2.1.1'

	// MySQL Database
	implementation 'mysql:mysql-connector-java:8.0.33'

	// Lombok
	compileOnly 'org.projectlombok:lombok:1.18.30'
	annotationProcessor 'org.projectlombok:lombok:1.18.30'

	// Google Cloud OAuth 인증 및 HTTP 클라이언트
	implementation 'com.google.auth:google-auth-library-oauth2-http:1.19.0'
	implementation 'com.google.http-client:google-http-client-gson:1.43.3'

	// 외부 API 파싱용 추가 라이브러리
	implementation 'org.jsoup:jsoup:1.17.2'
	implementation 'org.json:json:20231013'

	// Test
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
}

def frontendDir = "$projectDir/src/main/frontend"

sourceSets {
	main {
		resources {
			srcDirs = ["$projectDir/src/main/resources"]
		}
	}
}

// Frontend 빌드 작업
tasks.register("installReact", Exec) {
	workingDir = file(frontendDir)
	inputs.dir(frontendDir)
	group = BasePlugin.BUILD_GROUP

	if (System.getProperty('os.name').toLowerCase().contains('windows')) {
		commandLine "cmd", "/c", "npm install"
	} else {
		commandLine "sh", "-c", "npm install"
	}
}

tasks.register("buildReact", Exec) {
	dependsOn("installReact")
	workingDir = file(frontendDir)
	inputs.dir(frontendDir)
	group = BasePlugin.BUILD_GROUP

	if (System.getProperty('os.name').toLowerCase().contains('windows')) {
		commandLine "cmd", "/c", "npm run build"
	} else {
		commandLine "sh", "-c", "npm run build"
	}
}

tasks.register("copyReactBuildFiles", Copy) {
	dependsOn("buildReact")
	from("$frontendDir/build")
	into("$projectDir/src/main/resources/static")
}

// React 빌드를 리소스 복사에 연결
processResources {
	dependsOn "copyReactBuildFiles"
}
